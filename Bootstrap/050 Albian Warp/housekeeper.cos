new: simp 1 1 35700 "blnk" 1 0 0
tick 200
*ov00 is going to account for RACE-- the time the last tick took in MS
setv ov00 0
scrp 1 1 35700 9
inst
*if race is less than 50, we're in fast ticks, so don't go on until you
*total is at least 50 (should be about the equivlent of not being in fast ticks).
	addv ov00 race
	doif ov00 lt 50
		stop
	else
		setv ov00 0
	endi

	doif game "aw_online_indicator" eq 0 and game "aw_status" eq "online"
		sets game "aw_status" "offline"
		sets game "status" "offline"

		enum 1 2 14
			mesg writ targ 501
		next
	elif game "aw_online_indicator" eq 1 and game "aw_status" eq "offline"
		sets game "aw_status" "online"
		sets game "status" "online"
		enum 1 2 14
			mesg writ targ 500
		next
	endi
	setv game "aw_online_indicator" 0

** This part here checks for available creatures
** with a "warp" chunktype!
** If the creature has a NAME variable "Pray Extra recipient_userID" that is the same
** as the GAME variable "user_of_this_world" it is then Imported!
** Because this import is a cloned import
	doif game "aw_status" eq "online"
		pray refr
		sets va00 ""
		doif pray coun "warp" gt 0
			outv pray coun "warp"
			outs "\n"
			sets va01 pray next "warp" va00
			sets va00 va01
			outs va01
			sets va10 pray agts va01 "Pray Extra recipient_userID" "Unknown"
			sets va11 pray agts va01 "Pray Extra sender_user_ID" "Unknown"
			outs "\n"
			outs va10
			outs "\n"
			outs va11
			doif va10 eq game "user_of_this_world"
				outs "this is a norn for me"
				enum 1 1 154
					mesg wrt+ targ 1001 va01 0 0
				next
				setv va99 pray impo va01 1 0
**eventually need to refine/replace this since coords can vary in coming in through portals!
				mvft 5250 9450
		        aslp 0
		        pose 59
		        stim writ targ 95 1
		        outs name "aw_creature_name"
		        outs gtos 0
		        hist name gtos 0 name "aw_creature_name"
			endi
		endi
	endi
endm

***
* Warp Creature to other Player
* _p1_ = creature (creature)
* _p2_ = recipient user (str)
***
scrp 1 1 35700 1000
	lock
	doif game "aw_status" eq "online"
* Target the Creature
		targ _p1_
* Get its moniker and save it to va01 (important to get the filename of the exported creature)
		sets va01 gtos 0
		sets name "aw_creature_monicer" va01
		sets va02 hist name va01
		sets va03 va02
		sets name "aw_creature_name" va02
		sets name "aw_sender" game "user_of_this_world"		
* Set the recipient user id on the Creature that will be warped
		sets name "Pray Extra recipient_userID" _p2_
* Set the sender user id on the Creature that will be warped
		sets name "Pray Extra sender_user_ID" game "user_of_this_world"
		sets name "Last Network User" game "user_of_this_world"
* export the Creature with the custom Chunk Type `WARP` (warp out)
		setv va99 pray expo "warp"
* Create a aw_message_object that tells the client the moniker of the exported creature
		doif va02 ne ""
			adds va02 "_"
			adds va02 va01
			sets va01 va02
		endi
		new: simp 1 1 35760 "blnk" 1 0 0
		sets name "aw_recipient" _p2_
		sets name "moniker" va01
		sets name "creature_name" va03
	endi
	unlk
endm

rscr
enum 1 1 35700
kill targ
next
