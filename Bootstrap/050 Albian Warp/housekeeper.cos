*setup CAOS functions

*Returns name of random online user
*no parameters
*use with sets va30 caos 0 0 0 0 game "f_aw_ruso" 0 1 va99
sets game "f_aw_ruso" sorc 1 1 35700 9000
setv game "f_aw_ruso:v" 1


*inject actual agent
new: simp 1 1 35700 "blnk" 1 0 0
tick 200
*ov00 is going to account for RACE-- the time the last tick took in MS
setv ov00 0


scrp 1 1 35700 9
inst
*if race is less than 50, we're in fast ticks, so don't go on until you
*total is at least 50 (should be about the equivlent of not being in fast ticks).
	addv ov00 race
	doif ov00 lt 50
		stop
	else
		setv ov00 0
	endi

	doif game "aw_online_indicator" eq 0 and game "aw_status" eq "online"
		sets game "aw_status" "offline"
		sets game "status" "offline"

		enum 1 2 14
			mesg writ targ 501
		next
	elif game "aw_online_indicator" eq 1 and game "aw_status" eq "offline"
		sets game "aw_status" "online"
		sets game "status" "online"
		enum 1 2 14
			mesg writ targ 500
		next
	endi
	setv game "aw_online_indicator" 0
endm

***
* Warp Creature to other Player
* _p1_ = creature (creature)
* _p2_ = recipient user (str)
***
scrp 1 1 35700 1000
	lock
	doif game "aw_status" eq "online"
* Target the Creature
		targ _p1_
* Get its moniker and save it to va01 (important to get the filename of the exported creature)
		sets va01 gtos 0
		sets name "aw_creature_monicer" va01
		sets va02 hist name va01
		sets name "aw_creature_name" va02
		sets name "aw_sender" game "user_of_this_world"
* Set the recipient user id on the Creature that will be warped
		sets name "Pray Extra recipient_userID" _p2_
* Set the sender user id on the Creature that will be warped
		sets name "Pray Extra sender_user_ID" game "user_of_this_world"
		sets name "Last Network User" game "user_of_this_world"
* export the Creature with the custom Chunk Type `WARP` (warp out)
		setv va99 pray expo "warp"
* Create a aw_message_object that tells the client the moniker of the exported creature
		new: simp 1 1 35760 "blnk" 1 0 0
		sets name "aw_recipient" _p2_
		sets name "moniker" va01
		sets name "creature_name" va02
	endi
	unlk
endm

***
* Delete Creature File from Export folder, containing the given moniker
* _p1_ = moniker (str)
***
scrp 1 1 35700 1337
	setv va01 _p1_
	adds va01 ".warp"
	dbg: outs "try to delete creature with moniker: "
	dbg: outs va01
	setv va00 pray kill va01
	dbg: outv va00
	outv va00
endm

scrp 1 1 35700 1001
*sets number of real, actual contacts into game "contact_count"
inst
*update the number of contacts you currently have
		sets va40 ""
		sets va41 "_contact"
		setv va45 0
		loop
			sets va42 gamn va40
*			see if string contains search
			setv va43 sins va42 1 va41
*			if it does, and is not an automation:
			doif va43 ne -1
				doif subs va42 1 1 ne "!" and subs va42 1 1 ne "_"
					addv va45 1
				endi
			endi
*	set 'previous entry' to current entry
			sets va40 va42
		untl va42 eq ""
		setv game "contact_count" va45
endm

*holder script for CAOS function: 
*no parameters-- just returns a random user
scrp 1 1 35700 9000
inst
*trigger the update contacts script
*I don't even think this DOES good in an inst but whatever
	rtar 1 1 35700
	mesg writ targ 1001
	targ ownr

	setv va48 game "contact_count"
	dbg: outs "there are this many contacts"
	dbg: outv va48


	setv va49 rand 1 va48

	dbg: outs "random number is:"
	dbg: outv va48

	sets va30 ""
	sets va40 ""
	sets va47 ""
	sets va41 "_contact"
	setv va45 0
	loop
		sets va42 gamn va40
*		see if string contains search
*		and also is an actual contact
		doif va42 ne ""

			setv va43 sins va42 1 va41
*							if it does
			doif va43 ne -1
				doif subs va42 1 1 ne "!" and subs va42 1 1 ne "_"
					dbg: outs va42
					addv va45 1
					dbg: outv va45
					doif va45 eq va49
						sets va30 va42
					endi
				endi
			endi

		endi
*		set 'previous entry' to current entry
		sets va40 va42
	untl va42 eq ""

*					va30 now holds your random contact with "_contact" appended
*					You need to chop that off
	setv va66 strl va30
	subv va66 8
	sets va30 subs va30 1 va66
	dbg: outs "Random contact chosen: "
	dbg: outs va30
	outs va30
endm

rscr
enum 1 1 35700
kill targ
next
