*+--------------------+
*|                    |
*|    GUI: TopLeft    |
*|                    |
*+--------------------+
*
* The C3 GUI cos file referred to this part of the GUI as "the topleft parts" or something very similar! Well, I've taken that nickname
* and made it the actual name for these bits! :)
*
*
** Create the GUI TopLeft

new: comp 1 2 14 "ds gui" 1 3 8900
imsk 8
attr 308
perm 60
clac -1
** TopLeft Background
pat: dull 1 "ds gui" 32 6 6 -1
part 1
alph 128 1
** Selected Creature (Button)
pat: butt 2 "blnk" 0 1 6 3 0 [] 1001 0
** Selected Creature Name (Display)
pat: fixd 3 "ds gui" 12 8 75 0 "WhiteOnTransparentChars"
part 3
frmt 2 2 0 0 0 0 0
** Selected Creature Gender (Display)
pat: dull 4 "gender" 0 33 50 10
** Load Creature History for Selected Creature (Button)
pat: butt 5 "ds gui" 12 1 8 75 0 [] 1002 0
** Agent Help (Button)
pat: butt 6 "ds gui" 5 2 72 9 1 [] 1000 0
part 6
pose 0
** Connect to Creature Labs (Button)
pat: butt 7 "ds gui" 56 2 109 75 1 [] 1003 0
** Network Status (Display)
pat: butt 8 "ds gui" 68 9 101 68 0 [] 1003 0
part 8
** Save to a game Variable
seta game "ds_gui_topleft" targ
flto 0 -3

** The first few scripts below this point are mostly unchanged from Creatures 3.

** Creature History for Selected Creature (change name)
scrp 1 2 14 1002
	inst
	doif norn ne null
		targ norn
		seta va05 targ
		new: comp 1 2 23 "ds_creature_history" 32 0 8520
		mesg wrt+ targ 1000 va05 1001 0
	endi
endm

*********icons creature data change
scrp 1 2 14 127
	inst
	doif norn ne null
		rtar 1 2 13
		doif targ ne null
			part 4
			pose 1
		endi
		targ norn
*string value sprite file name
		sets va98 face
*image number of face
		setv va99 face
*pigmentation
		setv va41 tint 1
		setv va42 tint 2
		setv va43 tint 3
		setv va44 tint 4
		setv va45 tint 5
*get the moniker
		sets va01 gtos 0
		doif ooww va01 eq 3
*get the name
			sets va02 hist name va01
			doif va02 eq ""
				sets va02 "?"
			endi

*display the name
			targ ownr
			part 3
			ptxt va02

*display the face
			part 2
			doif gall <> va98 or abba <> va99
				gall va98 va99
				doif game "creature_hud_tint" = 1
					tino va41 va42 va43 va44 va45
				endi
			endi
		else
			norn null
			targ ownr
			part 2
			gall "blnk" 0
			part 3
			ptxt ""
			part 4
			pose 0
		endi
	else
		targ ownr
		part 2
		gall "blnk" 0
		part 3
		ptxt ""
		part 4
		pose 0
	endi
	rtar 1 2 35
	doif targ ne null
		mesg writ targ 120
	endi
endm
*******icons enter scope
scrp 1 2 14 8
	mesg writ ownr 127
endm

**********ICONS selected creature changed event
scrp 1 2 14 120
	inst
	doif norn ne null
* camera to and track the norn
		targ norn
		trck null 0 0 0 0
		doif hhld <> targ
			doif room targ <> -1 and carr <> game "c3_inventory"
				cmrt 0
			endi
		endi
		trck norn 70 70 1 0


*mae the export button available
		rtar 1 2 13
		doif targ ne null
			part 4
			pose 1
		endi
		targ norn
*string value sprite file name
		sets va98 face
*image number of face
		setv va99 face
*pigmentation
		setv va41 tint 1
		setv va42 tint 2
		setv va43 tint 3
		setv va44 tint 4
		setv va45 tint 5
*get the gender
		setv va97 spcs
*get the moniker
		sets va01 gtos 0
*get the name
		sets va02 hist name va01
		doif va02 eq ""
			sets va02 "?"
		endi

*display the name
		targ ownr
		part 3
		ptxt va02
*display the gender
		part 4
		pose va97
*display the face
		part 2
		doif gall <> va98 or abba <> va99
			gall va98 va99
			doif game "creature_hud_tint" = 1
				tino va41 va42 va43 va44 va45
			endi
		endi
	else
**otherwise no selected creature

		targ ownr
		part 2
		gall "blnk" 0
		part 3
		ptxt ""
		part 4
		pose 0
	endi
endm

*HELP mode button
scrp 1 2 14 1000
**OV00 is whether in help mode or normal
*0=normal, 1=help mode
	targ pntr
	doif held ne null
		sndc "buzz"
	else
		targ ownr
		doif ov00 eq 0
			setv ov00 1
			part 6
			pose 1
			wait 1
*	target the help watcher
			rtar 1 2 4
			mesg wrt+ targ 73 112 0 0
		else
			setv ov00 0
			part 6
			pose 0
			wait 1
			rtar 1 2 4
			mesg wrt+ targ 73 112 0 0
		endi
	endi
endm
***Selected Creature face
scrp 1 2 14 1001
	inst
	doif norn ne null
		targ norn
		doif room targ <> -1 and carr <> game "c3_inventory"
			cmrt 0
		endi
	else
		sndc "buzz"
	endi
endm
scrp 1 2 14 76
	inst
	doif _p1_ = 2
		doif pure = 0
			targ pntr
			doif held = null
				doif hots = ownr
					rtar 1 2 6
					doif targ <> null
						mesg wrt+ targ 73 9 0 0
					endi
				endi
			endi
		endi
	endi
endm


** Scripts for agent 1 2 14 after this point are new for Docking Station. Login / Connect GUI Scripts are all located
** in the zzz_gamestart_login.cos file.


scrp 1 2 14 1003
	inst
	* call the go offline script
	enum 1 1 35700
		mesg wrt+ targ 1100 0 0 0
	next
endm


* Set Online Indicator to ONLINE
scrp 1 2 14 500
	call 1004 0 0
	snde "yes_"
	part 7
	pose 1
	call 1005 0 0
endm

* Set Online Indicator to OFFLINE
scrp 1 2 14 501
	call 1004 0 0
	snde "no__"
	part 7
	pose 0
	enum 1 1 210
		kill targ
	next
	enum 1 1 217
		kill targ
	next
	call 1005 0 0
endm

* creature arriving event - display icon
scrp 1 2 14 1004
	inst
	part 8
	anim [1 2 3 4 5 6 7 8 255]
endm

* cancel creature arriving warning
scrp 1 2 14 1005
	inst
	part 8
	pose 0
endm


rscr
** Kill the GUI TopLeft
enum 1 2 14
	kill targ
next
scrx 1 2 14 1002
scrx 1 2 14 127
scrx 1 2 14 8
scrx 1 2 14 120
scrx 1 2 14 120
scrx 1 2 14 1000
scrx 1 2 14 1001
scrx 1 2 14 1002
scrx 1 2 14 1003
scrx 1 2 14 1004
scrx 1 2 14 1005
scrx 1 2 14 76
scrx 1 2 14 500
scrx 1 2 14 501